bind(root) := (
    isDefined(parameter, parameterNames) := (
        parameterName := getLexeme(getTag(parameter))
        !(parameterName =:= "_") /\ parameterNames.contains(parameterName) \/
            isJust(getBuiltin(parameter))
    )

    bind'(node, parameterNames) := (
        node.isLambda ? (
            parameter := getParameter(node)
            isDefined(parameter, parameterNames) ?
                syntaxError("symbol already defined", parameter)
            parameterName := getLexeme(getTag(parameter))
            newBody := bind'(getBody(node), parameterName :: parameterNames)
            newLambda(getTag(node), parameter, newBody)
        )
        node.isApplication ? (
            newFunction := bind'(getFunction(node), parameterNames)
            newArgument := bind'(getArgument(node), parameterNames)
            newApplication(getTag(node), newFunction, newArgument)
        )
        node.isSymbol ? (
            tag := getTag(node)
            lexeme := getLexeme(tag)
            lexeme =:= "_" /\ getLine(tag) > 0 ?
                syntaxError("cannot reference", node)
            getBuiltin(node)?.(x -> just(newBuiltin(getTag(node), x))) ?:
                newReference(getTag(node), parameterNames.indexOf(lexeme) ?:
                syntaxError("undefined symbol", node))
        )
        node
    )
    bind'(root, [])
)
