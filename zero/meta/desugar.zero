
isDefinition(node) := isApplication(node) /\ getLexeme(node) =:= ":="

hasRecursiveCalls(node, name) := (
    isLambda(node) ? (
        getLexeme(getParameter(node)) =:= getLexeme(name) ? (
            isInternal(getParameter(node)) ? false
            syntaxError("symbol already defined", getParameter(node))
        )
        hasRecursiveCalls(getBody(node), name)
    )
    isApplication(node) ?
        hasRecursiveCalls(getFunction(node), name) \/
        hasRecursiveCalls(getArgument(node), name)
    isSymbol(node) \/ isReference(node) ?
        getLexeme(node) =:= getLexeme(name)
    false
)

transformRecursion(name, value) := (
    !isSymbol(name) \/ !hasRecursiveCalls(value, name) ? value
    newApplication(getTag(name), yCombinator,
        newLambda(getTag(name), newName(getTag(name)), value))
)

getNameAndValue(left, right) := (
    !isApplication(left) ? (left, transformRecursion(left, right))
    getNameAndValue(getFunction(left),
        newLambda(getTag(getArgument(left)), getArgument(left), right))
)

transformDefinition(definition, explicitScope) := (
    (name, value) := getNameAndValue(
        getFunction(definition), getArgument(definition))
    scope := isEOF(explicitScope) ? name || explicitScope
    transform := newLambda(getTag(definition), name, scope)
    newApplication(getTag(name), transform, value)
)

desugar(node) := (
    tag := getTag(node)
    isLambda(node) ? newLambda(tag, getParameter(node), desugar(getBody(node)))
    isApplication(node) ? (
        isDefinition(node) ? (
            isDefinition(getFunction(node)) ?
                syntaxError("cannot define a definition", node)
            desugar(transformDefinition(node, eof))
        )
        isDefinition(getFunction(node)) ?
            transformDefinition(getFunction(node), desugar(getArgument(node)))
        isDefinition(getArgument(node)) ?
            transformDefinition(getArgument(node), eof)
        newApplication(getTag(node),
            desugar(getFunction(node)), desugar(getArgument(node)))
    )
    node        // all leaf nodes are returned unmodified
)
